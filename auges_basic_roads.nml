/**
 */

grf {
	grfid: "AU\04\01";
	name: string(STR_GRF_NAME);
	desc: string(STR_GRF_DESCRIPTION);
	version: 1;
	min_compatible_version: 1;
	param 0 {
		param_road_style {
			type: int;
			name: string(STR_PARAM_ROAD_STYLE_NAME);
			desc: string(STR_PARAM_ROAD_STYLE_DESC);
			min_value: 0;
			max_value: 1;
			def_value: 0;
			names: {
				0: string(STR_PARAM_ROAD_STYLE_1);
				1: string(STR_PARAM_ROAD_STYLE_2);
			};
		}
	}
}

// Version and Platform check
if (ttd_platform == PLATFORM_TTDPATCH) {
	error(FATAL, string(STR_WRONG_PLATFORM));
}

// supported version
if (openttd_version < version_openttd(1, 10, 0, 0)) {
	error(FATAL, string(STR_WRONG_OPENTTD_VERSION));
}


// Type Tables
// -----------

roadtypetable {
	ROAD, ELRD, FOOT, PEDZ, DIRT, PTRD, PETR, ATRD, AETR, PCRD, CCRD, ACRD, CHWY, AHWY, ASPT, ELAS, HWAY
}

// Default ground tile template (re-use as needed)
template ground_tile(x, y) {
	[x, y, 64, 31, -31, 0]
}

// Template for underlays; 2x straight road, 5x junctions, 4x corners, 4x slope, 4x half-tile road
// Used for bridge surfaces also, therefore the template is split

template tmpl_underlay_straight() {
	ground_tile( 10, 10)
	ground_tile( 80, 10)
}
template tmpl_underlay_junctions() {
	ground_tile(150, 10)
	ground_tile(220, 10)
	ground_tile(290, 10)
	ground_tile(360, 10)
	ground_tile( 10, 50)
}
template tmpl_underlay_corners() {
	ground_tile( 80, 50)
	ground_tile(150, 50)
	ground_tile(220, 50)
	ground_tile(290, 50)
}

template tmpl_underlay_slope() {
	[150, 170, 64, 39, -31, -8]
	[220, 170, 64, 23, -31, 0]
	[290, 170, 64, 23, -31, 0]
	[360, 170, 64, 39, -31, -8]
}
template tmpl_underlay_half_tiles() {
	ground_tile(360, 50)
	ground_tile( 10, 90)
	ground_tile( 80, 90)
	ground_tile(150, 90)
}

template tmpl_underlay_roadtypes() {
	tmpl_underlay_straight()
	tmpl_underlay_junctions()
	tmpl_underlay_corners()
	tmpl_underlay_slope()
	tmpl_underlay_half_tiles()
	/* X-crossing */
	///ground_tile(0, 120)
	/* underlay for crossings w/o tracks */
	//ground_tile(  0, 130)
	///ground_tile( 10, 130)
	//ground_tile(225, 130)
	///ground_tile(220, 130)
	///ground_tile(150, 130)
	//ground_tile( 75, 130)
	///ground_tile( 80, 130)
}

spriteset(foot_path_underlay, "gfx/foot-path/foot_under.png") {
	tmpl_underlay_roadtypes()
}
spriteset(foot_path_underlay_snow, "gfx/foot-path/foot_under_snow.png") {
	tmpl_underlay_roadtypes()
}
//switch based on terrain type in arctic climate
switch(FEAT_ROADTYPES, SELF, underlay_foot_path, terrain_type) {
	TILETYPE_SNOW: foot_path_underlay_snow;
	default: foot_path_underlay;
}
spriteset(underlay_precinct, "gfx/pedestrian-zone/pedz_under.png") {
	tmpl_underlay_roadtypes()
}
spriteset(dirt_road_underlay, "gfx/dirt-road/dirt_under.png") {
	tmpl_underlay_roadtypes()
}
spriteset(dirt_road_underlay_snow, "gfx/dirt-road/dirt_under_snow.png") {
	tmpl_underlay_roadtypes()
}
//switch based on terrain type in arctic climate
switch(FEAT_ROADTYPES, SELF, underlay_dirt_road, terrain_type) {
	TILETYPE_SNOW: dirt_road_underlay_snow;
	default: dirt_road_underlay;
}
spriteset(underlay_concrete_siding_road, "gfx/siding-road-concrete/csrd_under.png") {
	tmpl_underlay_roadtypes()
}
spriteset(underlay_paved_town_road, "gfx/town-road-paved/ptrd_under.png") {
	tmpl_underlay_roadtypes()
}
spriteset(underlay_asphalt_town_road, "gfx/town-road-asphalt/atrd_under.png") {
	tmpl_underlay_roadtypes()
}
spriteset(underlay_paved_country_road, "gfx/country-road-paved/pcrd_under.png") {
	tmpl_underlay_roadtypes()
}
spriteset(underlay_asphalt_country_road, "gfx/country-road-asphalt/acrd_under.png") {
	tmpl_underlay_roadtypes()
}
spriteset(concrete_highway_europe_underlay, "gfx/highway-concrete/hway_concrete_europe_under.png") {
	tmpl_underlay_roadtypes()
}
spriteset(concrete_highway_northamerica_underlay, "gfx/highway-concrete/hway_concrete_northamerica_under.png") {
	tmpl_underlay_roadtypes()
}
switch (FEAT_ROADTYPES, SELF, concrete_highway_underlay, param_road_style) {
	1:concrete_highway_northamerica_underlay;
	default:concrete_highway_europe_underlay;
}
spriteset(asphalt_highway_northamerica_underlay, "gfx/highway-asphalt/hway_asphalt_northamerica_over.png") {
	tmpl_underlay_roadtypes()
}
spriteset(asphalt_highway_europe_underlay, "gfx/highway-asphalt/hway_asphalt_europe_over.png") {
	tmpl_underlay_roadtypes()
}
switch (FEAT_ROADTYPES, SELF, asphalt_highway_underlay, param_road_style) {
	1:asphalt_highway_northamerica_underlay;
	default:asphalt_highway_europe_underlay;
}

// overlays
template tmpl_overlay_roadtypes() {
	[ 10, 10, 64, 31, -31, 0]
	[ 80, 10, 64, 31, -31, 0]

	[150, 10, 64, 31, -31, 0]
	[220, 10, 64, 31, -31, 0]
	[290, 10, 64, 31, -31, 0]
	[360, 10, 64, 31, -31, 0]
	[ 10, 50, 64, 31, -31, 0]

	[ 80, 50, 64, 31, -31, 0]
	[150, 50, 64, 31, -31, 0]
	[220, 50, 64, 31, -31, 0]
	[290, 50, 64, 31, -31, 0]

	[150, 170, 64, 39, -31, -8]
	[220, 170, 64, 21, -31, 0]
	[290, 170, 64, 21, -31, 0]
	[360, 170, 64, 39, -31, -8]

	[360, 50, 64, 31, -31, 0]
	[ 10, 90, 64, 31, -31, 0]
	[ 80, 90, 64, 31, -31, 0]
	[150, 90, 64, 31, -31, 0]
}

spriteset(no_deco, "gfx/decorations/road_empty.png") {
	tmpl_overlay_roadtypes()
}
spriteset(overlay_precinct, "gfx/pedestrian-zone/pedz_under.png") {
	tmpl_overlay_roadtypes()
}
spriteset(dirt_road_overlay, "gfx/dirt-road/dirt_road_over.png") {
	tmpl_overlay_roadtypes()
}
spriteset(paved_town_road_rhd_overlay_0, "gfx/town-road-paved/ptrd_over_0_rhd.png") {
	//countryside roads
	tmpl_overlay_roadtypes()
}
spriteset(paved_town_road_lhd_overlay_0, "gfx/town-road-paved/ptrd_over_0_lhd.png") {
	//countryside roads
	tmpl_overlay_roadtypes()
}
switch (FEAT_ROADTYPES, SELF, paved_town_road_overlay_0, traffic_side) {
	TRAFFIC_SIDE_RIGHT: paved_town_road_rhd_overlay_0;
	paved_town_road_lhd_overlay_0;
}
spriteset(paved_town_road_rhd_overlay_1, "gfx/town-road-paved/ptrd_over_1_rhd.png") {
	//suburban roads
	tmpl_overlay_roadtypes()
}
spriteset(paved_town_road_lhd_overlay_1, "gfx/town-road-paved/ptrd_over_1_lhd.png") {
	//suburban roads
	tmpl_overlay_roadtypes()
}
switch (FEAT_ROADTYPES, SELF, paved_town_road_overlay_1, traffic_side) {
	TRAFFIC_SIDE_RIGHT: paved_town_road_rhd_overlay_1;
	paved_town_road_lhd_overlay_1;
}
spriteset(paved_town_road_overlay_2, "gfx/town-road-paved/ptrd_over_2_bth.png") {
	//town streets
	tmpl_overlay_roadtypes()
}
spriteset(paved_town_road_overlay_3, "gfx/town-road-paved/ptrd_over_3_bth.png") {
	//city roads
	tmpl_overlay_roadtypes()
}
spriteset(paved_town_road_overlay_4, "gfx/town-road-paved/ptrd_over_4_bth.png") {
	//city center streets
	tmpl_overlay_roadtypes()
}
switch(FEAT_ROADTYPES, SELF, overlay_paved_town_road, town_zone) {
	//switch based on town zone
	4: paved_town_road_overlay_4;
	3: paved_town_road_overlay_3;
	2: paved_town_road_overlay_2;
	1: paved_town_road_overlay_1;
	default: paved_town_road_overlay_0;
}
spriteset(asphalt_town_road_northamerica_rhd_overlay_0, "gfx/town-road-asphalt/atrd_northamerica_over_0_rhd.png") {
	//countryside roads
	tmpl_overlay_roadtypes()
}
spriteset(asphalt_town_road_northamerica_lhd_overlay_0, "gfx/town-road-asphalt/atrd_northamerica_over_0_lhd.png") {
	//countryside roads
	tmpl_overlay_roadtypes()
}
switch (FEAT_ROADTYPES, SELF, asphalt_town_road_northamerica_overlay_0, traffic_side) {
	TRAFFIC_SIDE_RIGHT: asphalt_town_road_northamerica_rhd_overlay_0;
	asphalt_town_road_northamerica_lhd_overlay_0;
}
spriteset(asphalt_town_road_northamerica_rhd_overlay_1, "gfx/town-road-asphalt/atrd_northamerica_over_1_rhd.png") {
	//suburban roads
	tmpl_overlay_roadtypes()
}
spriteset(asphalt_town_road_northamerica_lhd_overlay_1, "gfx/town-road-asphalt/atrd_northamerica_over_1_lhd.png") {
	//suburban roads
	tmpl_overlay_roadtypes()
}
switch (FEAT_ROADTYPES, SELF, asphalt_town_road_northamerica_overlay_1, traffic_side) {
	TRAFFIC_SIDE_RIGHT: asphalt_town_road_northamerica_rhd_overlay_1;
	asphalt_town_road_northamerica_lhd_overlay_1;
}
spriteset(asphalt_town_road_northamerica_overlay_2, "gfx/town-road-asphalt/atrd_northamerica_over_2_bth.png") {
	//town streets
	tmpl_overlay_roadtypes()
}
spriteset(asphalt_town_road_northamerica_overlay_3, "gfx/town-road-asphalt/atrd_northamerica_over_3_bth.png") {
	//city roads
	tmpl_overlay_roadtypes()
}
spriteset(asphalt_town_road_northamerica_overlay_4, "gfx/town-road-asphalt/atrd_northamerica_over_4_bth.png") {
	//city center streets
	tmpl_overlay_roadtypes()
}
switch(FEAT_ROADTYPES, SELF, switch_asphalt_zones_northamerica, town_zone) {
	//switch based on town zone
	4: asphalt_town_road_northamerica_overlay_4;
	3: asphalt_town_road_northamerica_overlay_3;
	2: asphalt_town_road_northamerica_overlay_2;
	1: asphalt_town_road_northamerica_overlay_1;
	default: asphalt_town_road_northamerica_overlay_0;
}
spriteset(asphalt_town_road_europe_rhd_overlay_0, "gfx/town-road-asphalt/atrd_europe_over_0_rhd.png") {
	//countryside roads
	tmpl_overlay_roadtypes()
}
spriteset(asphalt_town_road_europe_lhd_overlay_0, "gfx/town-road-asphalt/atrd_europe_over_0_lhd.png") {
	//countryside roads
	tmpl_overlay_roadtypes()
}
switch (FEAT_ROADTYPES, SELF, asphalt_town_road_europe_overlay_0, traffic_side) {
	TRAFFIC_SIDE_RIGHT: asphalt_town_road_europe_rhd_overlay_0;
	asphalt_town_road_europe_lhd_overlay_0;
}
spriteset(asphalt_town_road_europe_rhd_overlay_1, "gfx/town-road-asphalt/atrd_europe_over_1_rhd.png") {
	//suburban roads
	tmpl_overlay_roadtypes()
}
spriteset(asphalt_town_road_europe_lhd_overlay_1, "gfx/town-road-asphalt/atrd_europe_over_1_lhd.png") {
	//suburban roads
	tmpl_overlay_roadtypes()
}
switch (FEAT_ROADTYPES, SELF, asphalt_town_road_europe_overlay_1, traffic_side) {
	TRAFFIC_SIDE_RIGHT: asphalt_town_road_europe_rhd_overlay_1;
	asphalt_town_road_europe_lhd_overlay_1;
}
spriteset(asphalt_town_road_europe_overlay_2, "gfx/town-road-asphalt/atrd_europe_over_2_bth.png") {
	//town streets
	tmpl_overlay_roadtypes()
}
spriteset(asphalt_town_road_europe_overlay_3, "gfx/town-road-asphalt/atrd_europe_over_3_bth.png") {
	//city roads
	tmpl_overlay_roadtypes()
}
spriteset(asphalt_town_road_europe_overlay_4, "gfx/town-road-asphalt/atrd_europe_over_4_bth.png") {
	//city center streets
	tmpl_overlay_roadtypes()
}
switch(FEAT_ROADTYPES, SELF, switch_asphalt_zones_europe, town_zone) {
	//switch based on town zone
	4: asphalt_town_road_europe_overlay_4;
	3: asphalt_town_road_europe_overlay_3;
	2: asphalt_town_road_europe_overlay_2;
	1: asphalt_town_road_europe_overlay_1;
	default: asphalt_town_road_europe_overlay_0;
}
switch(FEAT_ROADTYPES, SELF, overlay_asphalt_town_road, param_road_style) {
	1: switch_asphalt_zones_northamerica;
	default: switch_asphalt_zones_europe;
}
spriteset(country_road_overlay_snow, "gfx/decorations/road_deco_snow_scattered.png") {
	tmpl_overlay_roadtypes()
}
spriteset(country_road_asphalt_overlay_snow, "gfx/decorations/road_deco_snow_ca_scattered.png") {
	tmpl_overlay_roadtypes()
}
//switches for concrete siding road based on terrain type
switch(FEAT_ROADTYPES, SELF, overlay_concrete_siding_road, terrain_type) {
	TILETYPE_SNOW: country_road_overlay_snow;
	default: no_deco;
}
//switches for country roads based on terrain type
switch(FEAT_ROADTYPES, SELF, overlay_paved_country_road, terrain_type) {
	TILETYPE_SNOW: country_road_overlay_snow;
	default: no_deco;
}
switch(FEAT_ROADTYPES, SELF, overlay_asphalt_country_road, terrain_type) {
	TILETYPE_SNOW: country_road_asphalt_overlay_snow;
	default: no_deco;
}
/*
spriteset(concrete_highway_europe_overlay, "gfx/highway-concrete/hway_concrete_europe_over.png") {
	tmpl_overlay_roadtypes()
}
spriteset(concrete_highway_northamerica_overlay, "gfx/highway-concrete/hway_concrete_northamerica_over.png") {
	tmpl_overlay_roadtypes()
}
switch(FEAT_ROADTYPES, SELF, concrete_highway_overlay, param_road_style) {
	1: concrete_highway_northamerica_overlay;
	default: concrete_highway_europe_overlay;
}
*/


// Template for depot sprites
template tmpl_depot() {
	[150, 218, 64, 61, -31, -30]
	[220, 218, 64, 61, -1, -45]
	[ 10, 218, 64, 61, -31, -30]
	[ 80, 218, 64, 61, -61, -45]
	[290, 218, 64, 61, -61, -45]
	[360, 218, 64, 61, -1, -45]
}
spriteset(depots_foot_path, "gfx/foot-path/foot_under.png") {
	tmpl_depot()
}
spriteset(depots_precinct, "gfx/pedestrian-zone/pedz_under.png") {
	tmpl_depot()
}
spriteset(depots_dirt_road, "gfx/dirt-road/dirt_under.png") {
	tmpl_depot()
}
spriteset(depots_concrete_siding_road, "gfx/siding-road-concrete/csrd_under.png") {
	tmpl_depot()
}
spriteset(depots_paved_town_road, "gfx/town-road-paved/ptrd_under.png") {
	tmpl_depot()
}
spriteset(depots_asphalt_town_road, "gfx/town-road-asphalt/atrd_under.png") {
	tmpl_depot()
}
spriteset(depots_paved_country_road, "gfx/country-road-paved/pcrd_under.png") {
	tmpl_depot()
}
spriteset(depots_asphalt_country_road, "gfx/country-road-asphalt/acrd_under.png") {
	tmpl_depot()
}
spriteset(concrete_highway_depots, "gfx/highway-concrete/hway_concrete_europe_under.png") {
	tmpl_depot()
}
spriteset(asphalt_highway_depots, "gfx/highway-asphalt/hway_asphalt_europe_over.png") {
	tmpl_depot()
}


// Bridge surface, uses the same sprites as track underlays, but in a different order
template tmpl_bridge_straight() {
	ground_tile( 10, 288)
	ground_tile( 80, 288)
}
template tmpl_bridge_slope() {
	[150, 288, 64, 39, -31, -8]
	[220, 288, 64, 23, -31, 0]
	[290, 288, 64, 23, -31, 0]
	[360, 288, 64, 39, -31, -8]
}
template tmpl_bridge_surfaces() {
	tmpl_bridge_straight()
	tmpl_bridge_slope()
}
spriteset(foot_path_bridge_surfaces, "gfx/foot-path/foot_under.png") {
	tmpl_bridge_surfaces()
}
spriteset(foot_path_bridge_surfaces_snow, "gfx/foot-path/foot_under_snow.png") {
	tmpl_bridge_surfaces()
}
//switch based on terrain type
switch(FEAT_ROADTYPES, SELF, bridge_surfaces_foot_path, terrain_type) {
	TILETYPE_SNOW: foot_path_bridge_surfaces_snow; 
	default: foot_path_bridge_surfaces;
}
spriteset(bridge_surfaces_precinct, "gfx/pedestrian-zone/pedz_under.png") {
	tmpl_bridge_surfaces()
}
spriteset(dirt_road_bridge_surfaces, "gfx/dirt-road/dirt_under.png") {
	tmpl_bridge_surfaces()
}
spriteset(dirt_road_bridge_surfaces_snow, "gfx/dirt-road/dirt_under_snow.png") {
	tmpl_bridge_surfaces()
}
//switch based on terrain type
switch(FEAT_ROADTYPES, SELF, bridge_surfaces_dirt_road, terrain_type) {
	TILETYPE_SNOW: dirt_road_bridge_surfaces_snow; 
	default: dirt_road_bridge_surfaces;
}
spriteset(bridge_surfaces_concrete_siding_road, "gfx/siding-road-concrete/csrd_under.png") {
	tmpl_bridge_surfaces()
}
spriteset(bridge_surfaces_paved_town_road, "gfx/town-road-paved/ptrd_under.png") {
	tmpl_bridge_surfaces()
}
spriteset(bridge_surfaces_asphalt_town_road, "gfx/town-road-asphalt/atrd_under.png") {
	tmpl_bridge_surfaces()
}
spriteset(bridge_surfaces_paved_country_road, "gfx/country-road-paved/pcrd_under.png") {
	tmpl_bridge_surfaces()
}
spriteset(bridge_surfaces_asphalt_country_road, "gfx/country-road-asphalt/acrd_under.png") {
	tmpl_bridge_surfaces()
}
spriteset(concrete_highway_europe_bridge_surfaces, "gfx/highway-concrete/hway_concrete_europe_under.png") {
	tmpl_bridge_surfaces()
}
spriteset(concrete_highway_northamerica_bridge_surfaces, "gfx/highway-concrete/hway_concrete_northamerica_under.png") {
	tmpl_bridge_surfaces()
}
switch(FEAT_ROADTYPES, SELF, concrete_highway_bridge_surfaces, param_road_style) {
	1: concrete_highway_northamerica_bridge_surfaces;
	default: concrete_highway_europe_bridge_surfaces;
}
spriteset(asphalt_highway_northamerica_bridge_surfaces, "gfx/highway-asphalt/hway_asphalt_northamerica_over.png") {
	tmpl_bridge_surfaces()
}
spriteset(asphalt_highway_europe_bridge_surfaces, "gfx/highway-asphalt/hway_asphalt_europe_over.png") {
	tmpl_bridge_surfaces()
}
switch(FEAT_ROADTYPES, SELF, switch_asphalt_highway_bridge_surfaces, param_road_style) {
	1: asphalt_highway_northamerica_bridge_surfaces; 
	default: asphalt_highway_europe_bridge_surfaces;
}

// Roadstop sprites:
template tmpl_underlay_roadstop() {
	ground_tile(220, 90)
	ground_tile(290, 90)
	ground_tile(360, 90)
	ground_tile(10, 130)
}
spriteset(foot_path_roadstops, "gfx/foot-path/foot_under.png") {
	tmpl_underlay_roadstop()
}
spriteset(foot_path_roadstops_snow, "gfx/foot-path/foot_under_snow.png") {
	tmpl_underlay_roadstop()
}
//switch based on terrain type
switch(FEAT_ROADTYPES, SELF, roadstops_foot_path, terrain_type) {
	TILETYPE_SNOW: foot_path_roadstops_snow; 
	default: foot_path_roadstops;
}
spriteset(roadstops_precinct, "gfx/pedestrian-zone/pedz_under.png") {
	tmpl_underlay_roadstop()
}
spriteset(dirt_road_roadstops, "gfx/dirt-road/dirt_under.png") {
	tmpl_underlay_roadstop()
}
spriteset(dirt_road_roadstops_snow, "gfx/dirt-road/dirt_under_snow.png") {
	tmpl_underlay_roadstop()
}
//switch based on terrain type
switch(FEAT_ROADTYPES, SELF, roadstops_dirt_road, terrain_type) {
	TILETYPE_SNOW: dirt_road_roadstops_snow; 
	default: dirt_road_roadstops;
}
spriteset(roadstops_concrete_siding_road, "gfx/siding-road-concrete/csrd_under.png") {
	tmpl_underlay_roadstop()
}
spriteset(roadstops_paved_town_road, "gfx/town-road-paved/ptrd_under.png") {
	tmpl_underlay_roadstop()
}
spriteset(roadstops_asphalt_town_road, "gfx/town-road-asphalt/atrd_under.png") {
	tmpl_underlay_roadstop()
}
spriteset(roadstops_paved_country_road, "gfx/country-road-paved/pcrd_under.png") {
	tmpl_underlay_roadstop()
}
spriteset(roadstops_asphalt_country_road, "gfx/country-road-asphalt/acrd_under.png") {
	tmpl_underlay_roadstop()
}
spriteset(concrete_highway_roadstops, "gfx/highway-concrete/hway_concrete_europe_under.png") {
	tmpl_underlay_roadstop()
}
spriteset(asphalt_highway_roadstops, "gfx/highway-asphalt/hway_asphalt_europe_over.png") {
	tmpl_underlay_roadstop()
}

// catenary spritesets and definitions, trolley wires and marker posts
// regular trolley wire sprites 
template tmpl_wire_normal() {
	[ 10, 10, 64, 47, -31, -16]
	[ 80, 10, 64, 47, -31, -16]

	[150, 10, 64, 47, -31, -16]
	[220, 10, 64, 47, -31, -16]
	[290, 10, 64, 47, -31, -16]
	[360, 10, 64, 47, -31, -16]
	[ 10, 66, 64, 47, -31, -16]

	[ 80, 66, 64, 47, -31, -16]
	[150, 66, 64, 47, -31, -16]
	[220, 66, 64, 47, -31, -16]
	[290, 66, 64, 31, -31, -16]

	[150, 234, 64, 55, -31, -24]
	[220, 234, 64, 39, -31, -16]
	[290, 234, 64, 39, -31, -16]
	[360, 234, 64, 55, -31, -24]
// short ends
	[360,  66, 64, 47, -31, -16]
	[ 10, 122, 64, 47, -31, -16]
	[ 80, 122, 64, 47, -31, -16]
	[150, 122, 64, 47, -31, -16]

	[220, 122, 64, 31, -31, -16]
	[290, 122, 64, 31, -31, -16]
	[360, 122, 64, 31, -31, -16]
	[ 10, 178, 64, 31, -31, -16]
}
//trolley wire for bridges
template tmpl_wire_bridge() {
	[ 10, 298, 64, 47, -31, -16]
	[ 80, 298, 64, 47, -31, -16]
	[150, 298, 64, 55, -31, -24]
	[220, 298, 64, 39, -31, -16]
	[290, 298, 64, 39, -31, -16]
	[360, 298, 64, 55, -31, -24]
}
//combine the two to allow bridge to use different png - doesn't work :(
template tmpl_wire_roadtypes() {
	tmpl_wire_normal()
	tmpl_wire_bridge()
}

// define
spriteset(country_road_markers_rear, "gfx/decorations/country_marks_rear.png") {
	tmpl_wire_roadtypes()
}
spriteset(country_road_markers_front, "gfx/decorations/country_marks_front.png") {
	tmpl_wire_roadtypes()
}

item(FEAT_ROADTYPES, default_town_road) {
	property {
		label: "ROAD";
		roadtype_flags: bitmask(ROADTYPE_FLAG_HIDDEN);
		powered_roadtype_list: ["ROAD", "ASPT", "ELRD", "DIRT", "CSRD", "PTRD", "PETR", "ATRD", "AETR", "PCRD", "ACRD", "CHWY", "AHWY", "ELAS", "HWAY"];
	}
}
item(FEAT_ROADTYPES, foot_path) {
	property {
		name: string(STR_STREET_NAME_FOOTPATH);
		label: "FOOT";
		speed_limit: 4 km/h;
		introduction_date: date(0000,01,01);
		construction_cost: 1;
		maintenance_cost: 2;
		// ROAD, ELRD, FOOT, PEDZ, DIRT, CSRD, PTRD, PETR, ATRD, AETR, PCRD, ACRD, CHWY; AHWY, ASPT, ELAS, HWAY
		powered_roadtype_list: ["FOOT"];
		roadtype_flags: bitmask(ROADTYPE_FLAG_NO_LEVEL_CROSSING);
		toolbar_caption: string(STR_FOOT_PATH_TOOLBAR_CAPTION);
		menu_text: string(STR_FOOT_PATH_MENU_TEXT);
		build_window_caption: string(STR_GENERIC_BUILD_WINDOW_CAPTION);
		autoreplace_text: string(STR_GENERIC_AUTOREPLACE_TEXT);
		new_engine_text: string(STR_GENERIC_NEW_ENGINE_TEXT);
		sort_order: 1;
	}
	graphics {
		underlay: underlay_foot_path;
		depots: depots_foot_path;
		bridge_surfaces: bridge_surfaces_foot_path;
		roadstops: roadstops_foot_path;
	}
}
item(FEAT_ROADTYPES, precinct) {
	property {
		name: string(STR_STREET_NAME_PRECINCT);
		label: "PEDZ";
		speed_limit: 4 km/h;
		introduction_date: date(1960,01,01);
		construction_cost: 2;
		maintenance_cost: 3;
		// ROAD, ELRD, FOOT, PEDZ, DIRT, CSRD, PTRD, PETR, ATRD, AETR, PCRD, ACRD, CHWY; AHWY, ASPT, ELAS, HWAY
		powered_roadtype_list: ["FOOT", "PEDZ"];
		roadtype_flags: bitmask(ROADTYPE_FLAG_NO_LEVEL_CROSSING);
		toolbar_caption: string(STR_PRECINCT_TOOLBAR_CAPTION);
		menu_text: string(STR_PRECINCT_MENU_TEXT);
		build_window_caption: string(STR_GENERIC_BUILD_WINDOW_CAPTION);
		autoreplace_text: string(STR_GENERIC_AUTOREPLACE_TEXT);
		new_engine_text: string(STR_GENERIC_NEW_ENGINE_TEXT);
		sort_order: 2;
	}
	graphics {
		underlay: underlay_precinct;
		//track_overlay: overlay_precinct;
		depots: depots_precinct;
		bridge_surfaces: bridge_surfaces_precinct;
		roadstops: roadstops_precinct;
	}
}
item(FEAT_ROADTYPES, dirt_road) {
	property {
		name: string(STR_STREET_NAME_DIRT_SIDING_PATH);
		label: "DIRT";
		speed_limit: 30 km/h;
		introduction_date: date(0000,01,01);
		construction_cost: 3;
		maintenance_cost: 5;
		// ROAD, ELRD, FOOT, PEDZ, DIRT, CSRD, PTRD, PETR, ATRD, AETR, PCRD, ACRD, CHWY; AHWY, ASPT, ELAS, HWAY
		powered_roadtype_list: ["ROAD", "ASPT", "ELRD", "DIRT", "CSRD", "PTRD", "ATRD", "PCRD", "ACRD", "CHWY", "AHWY", "ELAS", "HWAY"];
		roadtype_flags: bitmask(ROADTYPE_FLAG_NO_HOUSES);
		toolbar_caption: string(STR_FARM_PATH_TOOLBAR_CAPTION);
		menu_text: string(STR_FARM_PATH_MENU_TEXT);
		build_window_caption: string(STR_GENERIC_BUILD_WINDOW_CAPTION);
		autoreplace_text: string(STR_GENERIC_AUTOREPLACE_TEXT);
		new_engine_text: string(STR_GENERIC_NEW_ENGINE_TEXT);
		sort_order: 3;
	}
	graphics {
		underlay: underlay_dirt_road;
		//track_overlay: overlay_dirt_road;
		depots: depots_dirt_road;
		bridge_surfaces: bridge_surfaces_dirt_road;
		roadstops: roadstops_dirt_road;
	}
}
item(FEAT_ROADTYPES, concrete_siding_road) {
	property {
		name: string(STR_STREET_NAME_CONCRETE_SIDING_ROAD);
		label: "CSRD";
		speed_limit: 50 km/h;
		introduction_date: date(1955,01,01);
		construction_cost: 6;
		maintenance_cost: 10;
		// ROAD, ELRD, FOOT, PEDZ, DIRT, CSRD, PTRD, PETR, ATRD, AETR, PCRD, ACRD, CHWY; AHWY, ASPT, ELAS, HWAY
		powered_roadtype_list: ["ROAD", "ASPT", "ELRD", "DIRT", "CSRD", "PTRD", "ATRD", "PCRD", "ACRD", "CHWY", "AHWY", "ELAS", "HWAY"];
		roadtype_flags: bitmask(ROADTYPE_FLAG_NO_HOUSES);
		toolbar_caption: string(STR_CONCRETE_SIDING_ROAD_TOOLBAR_CAPTION);
		menu_text: string(STR_CONCRETE_SIDING_ROAD_MENU_TEXT);
		build_window_caption: string(STR_GENERIC_BUILD_WINDOW_CAPTION);
		autoreplace_text: string(STR_GENERIC_AUTOREPLACE_TEXT);
		new_engine_text: string(STR_GENERIC_NEW_ENGINE_TEXT);
		sort_order: 4;
	}
	graphics {
		underlay: underlay_concrete_siding_road;
		track_overlay: overlay_concrete_siding_road;
		depots: depots_concrete_siding_road;
		bridge_surfaces: bridge_surfaces_concrete_siding_road;
		roadstops: roadstops_concrete_siding_road;
	}
}
item(FEAT_ROADTYPES, paved_town_road) {
	property {
		name: string(STR_STREET_NAME_PAVED_TOWN_ROAD);
		label: "PTRD";
		speed_limit: 40 km/h;
		introduction_date: date(0000,01,01);
		construction_cost: 8;
		maintenance_cost: 10;
		// ROAD, ELRD, FOOT, PEDZ, DIRT, CSRD, PTRD, PETR, ATRD, AETR, PCRD, ACRD, CHWY; AHWY, ASPT, ELAS, HWAY
		powered_roadtype_list: ["ROAD", "ASPT", "ELRD", "DIRT", "CSRD", "PTRD", "PETR", "ATRD", "AETR", "PCRD", "ACRD", "CHWY", "AHWY", "ELAS", "HWAY"];
		alternative_roadtype_list: ["ROAD"];
		roadtype_flags: bitmask(ROADTYPE_FLAG_TOWN_BUILD);
		toolbar_caption: string(STR_PAVED_TOWN_ROAD_TOOLBAR_CAPTION);
		menu_text: string(STR_PAVED_TOWN_ROAD_MENU_TEXT);
		build_window_caption: string(STR_GENERIC_BUILD_WINDOW_CAPTION);
		autoreplace_text: string(STR_GENERIC_AUTOREPLACE_TEXT);
		new_engine_text: string(STR_GENERIC_NEW_ENGINE_TEXT);
		sort_order: 5;
	}
	graphics {
		underlay: underlay_paved_town_road;
		track_overlay: overlay_paved_town_road;
		depots: depots_paved_town_road;
		bridge_surfaces: bridge_surfaces_paved_town_road;
		roadstops: roadstops_paved_town_road;
	}
}
item(FEAT_ROADTYPES, asphalt_town_road) {
	property {
		name: string(STR_STREET_NAME_ASPHALT_TOWN_ROAD);
		label: "ATRD";
		speed_limit: 50 km/h;
		introduction_date: date(1960,01,01);
		construction_cost: 16;
		maintenance_cost: 24;
		// ROAD, ELRD, FOOT, PEDZ, DIRT, CSRD, PTRD, PETR, ATRD, AETR, PCRD, CCRD, ACRD, CHWY; AHWY, ASPT, ELAS, HWAY
		powered_roadtype_list: ["ROAD", "ASPT", "ELRD", "DIRT", "CSRD", "PTRD", "PETR", "ATRD", "AETR", "PCRD", "ACRD", "CHWY", "AHWY", "ELAS", "HWAY"];
		roadtype_flags: bitmask(ROADTYPE_FLAG_TOWN_BUILD);
		toolbar_caption: string(STR_ASPHALT_TOWN_ROAD_TOOLBAR_CAPTION);
		menu_text: string(STR_ASPHALT_TOWN_ROAD_MENU_TEXT);
		build_window_caption: string(STR_GENERIC_BUILD_WINDOW_CAPTION);
		autoreplace_text: string(STR_GENERIC_AUTOREPLACE_TEXT);
		new_engine_text: string(STR_GENERIC_NEW_ENGINE_TEXT);
		sort_order: 6;
	}
	graphics {
		underlay: underlay_asphalt_town_road;
		track_overlay: overlay_asphalt_town_road;
		depots: depots_asphalt_town_road;
		bridge_surfaces: bridge_surfaces_asphalt_town_road;
		roadstops: roadstops_asphalt_town_road;
	}
}
item(FEAT_ROADTYPES, paved_country_road) {
	property {
		name: string(STR_STREET_NAME_PAVED_COUNTRY_ROAD);
		label: "PCRD";
		speed_limit: 70 km/h;
		introduction_date: date(0000,01,01);
		construction_cost: 12;
		maintenance_cost: 16;
		// ROAD, ELRD, FOOT, PEDZ, DIRT, CSRD, PTRD, PETR, ATRD, AETR, PCRD, CCRD, ACRD, CHWY; AHWY, ASPT, ELAS, HWAY
		powered_roadtype_list: ["ROAD", "ASPT", "ELRD", "DIRT", "CSRD", "PTRD", "ATRD", "PCRD", "ACRD", "CHWY", "AHWY", "ELAS", "HWAY"];
		roadtype_flags: bitmask(ROADTYPE_FLAG_NO_HOUSES, ROADTYPE_FLAG_CATENARY);
		toolbar_caption: string(STR_PAVED_COUNTRY_ROAD_TOOLBAR_CAPTION);
		menu_text: string(STR_PAVED_COUNTRY_ROAD_MENU_TEXT);
		build_window_caption: string(STR_GENERIC_BUILD_WINDOW_CAPTION);
		autoreplace_text: string(STR_GENERIC_AUTOREPLACE_TEXT);
		new_engine_text: string(STR_GENERIC_NEW_ENGINE_TEXT);
		sort_order: 7;
	}
	graphics {
		underlay: underlay_paved_country_road;
		track_overlay: overlay_paved_country_road;
		depots: depots_paved_country_road;
		bridge_surfaces: bridge_surfaces_paved_country_road;
		roadstops: roadstops_paved_country_road;
		catenary_back: country_road_markers_rear;
		catenary_front: country_road_markers_front;
	}
}
item(FEAT_ROADTYPES, asphalt_country_road) {
	property {
		name: string(STR_STREET_NAME_ASPHALT_COUNTRY_ROAD);
		label: "ACRD";
		speed_limit: 90 km/h;
		introduction_date: date(1970,01,01);
		construction_cost: 20;
		maintenance_cost: 28;
		// ROAD, ELRD, FOOT, PEDZ, DIRT, CSRD, PTRD, PETR, ATRD, AETR, PCRD, CCRD, ACRD, CHWY; AHWY, ASPT, ELAS, HWAY
		powered_roadtype_list: ["ROAD", "ASPT", "ELRD", "DIRT", "CSRD", "PTRD", "ATRD", "PCRD", "ACRD", "CHWY", "AHWY", "ELAS", "HWAY"];
		roadtype_flags: bitmask(ROADTYPE_FLAG_NO_HOUSES, ROADTYPE_FLAG_CATENARY);
		toolbar_caption: string(STR_ASPHALT_COUNTRY_ROAD_TOOLBAR_CAPTION);
		menu_text: string(STR_ASPHALT_COUNTRY_ROAD_MENU_TEXT);
		build_window_caption: string(STR_GENERIC_BUILD_WINDOW_CAPTION);
		autoreplace_text: string(STR_GENERIC_AUTOREPLACE_TEXT);
		new_engine_text: string(STR_GENERIC_NEW_ENGINE_TEXT);
		sort_order: 8;
	}
	graphics {
		underlay: underlay_asphalt_country_road;
		track_overlay: overlay_asphalt_country_road;
		depots: depots_asphalt_country_road;
		bridge_surfaces: bridge_surfaces_asphalt_country_road;
		roadstops: roadstops_asphalt_country_road;
		catenary_back: country_road_markers_rear;
		catenary_front: country_road_markers_front;
	}
}

item(FEAT_ROADTYPES, concrete_highway) {
	property {
		name: string(STR_STREET_NAME_CONCRETE_HIGHWAY);
		label: "CHWY";
		speed_limit: 90 km/h;
		introduction_date: date(1930,01,01);
		construction_cost: 24;
		maintenance_cost: 32;
		// ROAD, ELRD, FOOT, PEDZ, DIRT, CSRD, PTRD, PETR, ATRD, AETR, PCRD, ACRD, CHWY; AHWY, ASPT, ELAS, HWAY
		powered_roadtype_list: ["ROAD", "ASPT", "ELRD", "DIRT", "CSRD", "PTRD", "ATRD", "PCRD", "ACRD", "CHWY", "AHWY", "ELAS", "HWAY"];
		roadtype_flags: bitmask(ROADTYPE_FLAG_NO_HOUSES, ROADTYPE_FLAG_NO_LEVEL_CROSSING);
		toolbar_caption: string(STR_CONCRETE_HIGHWAY_TOOLBAR_CAPTION);
		menu_text: string(STR_CONCRETE_HIGHWAY_MENU_TEXT);
		build_window_caption: string(STR_GENERIC_BUILD_WINDOW_CAPTION);
		autoreplace_text: string(STR_GENERIC_AUTOREPLACE_TEXT);
		new_engine_text: string(STR_GENERIC_NEW_ENGINE_TEXT);
		sort_order: 9;
	}
	graphics {
		underlay: concrete_highway_underlay;
		//track_overlay: concrete_highway_overlay;
		depots: concrete_highway_depots;
		bridge_surfaces: concrete_highway_bridge_surfaces;
		roadstops: concrete_highway_roadstops;
	}
}
item(FEAT_ROADTYPES, asphalt_highway) {
	property {
		name: string(STR_STREET_NAME_ASPHALT_HIGHWAY);
		label: "AHWY";
		speed_limit: 120 km/h;
		introduction_date: date(1975,01,01);
		construction_cost: 28;
		maintenance_cost: 38;
		// ROAD, ELRD, FOOT, PEDZ, DIRT, CSRD, PTRD, PETR, ATRD, AETR, PCRD, ACRD, CHWY; AHWY, ASPT, ELAS, HWAY
		powered_roadtype_list: ["ROAD", "ASPT", "ELRD", "DIRT", "CSRD", "PTRD", "ATRD", "PCRD", "ACRD", "CHWY", "AHWY", "ELAS", "HWAY"];
		roadtype_flags: bitmask(ROADTYPE_FLAG_NO_HOUSES, ROADTYPE_FLAG_NO_LEVEL_CROSSING);
		toolbar_caption: string(STR_ASPHALT_HIGHWAY_TOOLBAR_CAPTION);
		menu_text: string(STR_ASPHALT_HIGHWAY_MENU_TEXT);
		build_window_caption: string(STR_GENERIC_BUILD_WINDOW_CAPTION);
		autoreplace_text: string(STR_GENERIC_AUTOREPLACE_TEXT);
		new_engine_text: string(STR_GENERIC_NEW_ENGINE_TEXT);
		sort_order: 10;
	}
	graphics {
		underlay: asphalt_highway_underlay;
		//track_overlay: switch_asphalt_zones;
		depots: asphalt_highway_depots;
		bridge_surfaces: switch_asphalt_highway_bridge_surfaces;
		roadstops: asphalt_highway_roadstops;
	}
}
